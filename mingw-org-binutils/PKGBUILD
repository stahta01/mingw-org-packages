# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Mateusz Mikula <mati865@gmail.com>

# MINGW_ARCH="mingw32" makepkg-mingw -Lf

PACKAGE_MINGW_ORG_PREFIX="mingw-org-$MSYSTEM_CARCH"
_target="i686-pc-mingw32"
_install_prefix="/mingw-org"

_realname=binutils
pkgbase=mingw-org-${_realname}
pkgname=("${PACKAGE_MINGW_ORG_PREFIX}-${_realname}")
pkgver=2.37
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (mingw-org)"
arch=('any')
mingw_arch=('mingw32')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${PACKAGE_MINGW_ORG_PREFIX}-toolchain")
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" 
  "${PACKAGE_MINGW_ORG_PREFIX}-cc"
  "${PACKAGE_MINGW_ORG_PREFIX}-libiconv"
  "${PACKAGE_MINGW_ORG_PREFIX}-libintl"
  "${PACKAGE_MINGW_ORG_PREFIX}-zlib"
)
options=('staticlibs' '!distcc' '!ccache' '!strip')
source=(https://ftp.gnu.org/gnu/binutils/${_realname}-${pkgver}.tar.xz
        0002-check-for-unusual-file-harder.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0110-binutils-mingw-gnu-print.patch
        0600-Change-uint-to-unsigned.patch
        0700-mingw-win9x-fseek-hack.patch
        0701-libiberty-define-for-headers-that-libiberty-cannot-f.patch
        0791-add-include-time-h.patch
        2001-ld-option-to-move-default-bases-under-4GB.patch
        2003-Restore-old-behaviour-of-windres-so-that-options-con.patch
        2004-bfd-Close-the-file-descriptor-if-there-is-no-archive.patch
        reproducible-import-libraries.patch
        specify-timestamp.patch)

sha256sums=('820d9724f020a3e69cb337893a0b63c2db161dadcb0e06fc11dc29eb1e84a32c'
            '2c99345fc575c3a060d6677537f636c6c4154fac0fde508070f3b6296c1060d4'
            '4e8ac055df61b1b5d6ae29dc87e1154737c2e87c7b244b44866702cabf1a5d18'
            '15fd46de15db03aacff360d15be1ddf2d81092db78aae1eb1e60f6756958745d'
            'b6ad3d4e73a2b075538b714c7512c75620b87820370ce138b1c90a9fb091dcca'
            '6e22603d3f47cf16441c21a266e7add6811d735fa6949cdcd1c4f8a3a6727f73'
            '530d50f66f7b36d33b6fa2a07718add7c617a6a28f908a1245a130491d81242d'
            '2b5586859d61e662edde3794aab7fd101d4bd776b7ace686de48a14f25a4e9d6'
            '675ea7c58b8b3fc628ea85843ee7ca7fb10137c2d42dbd407f83002cb98ff22d'
            'd584f1cd9e94cba0e9b27625c4acc8ad5242cd625c9b44839d42fc116072568c'
            '0ac95aec9bf5c3f4fac7d43df7cc56dc0ce123e06ada67025000323fed9dbf49'
            'a094660ec95996c00b598429843b7869037732146442af567ada9f539bd40480'
            '27696da8ecfff307537a461b205fad44d6abc1fa648fbf839e72a1d3ea71c40a')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93'
              '3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  apply_patch_with_msg \
    0002-check-for-unusual-file-harder.patch \
    0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch \
    0110-binutils-mingw-gnu-print.patch \
    0600-Change-uint-to-unsigned.patch \
    0700-mingw-win9x-fseek-hack.patch \
    0701-libiberty-define-for-headers-that-libiberty-cannot-f.patch \
    0791-add-include-time-h.patch

  # Add an option to change default bases back below 4GB to ease transition
  # https://github.com/msys2/MINGW-packages/issues/7027
  # https://github.com/msys2/MINGW-packages/issues/7023
  apply_patch_with_msg 2001-ld-option-to-move-default-bases-under-4GB.patch

  # https://github.com/msys2/MINGW-packages/pull/9233#issuecomment-889439433
  patch -R -p1 -i "${srcdir}/2003-Restore-old-behaviour-of-windres-so-that-options-con.patch"

  # patches for reproducibility from Debian:
  # https://salsa.debian.org/mingw-w64-team/binutils-mingw-w64/-/tree/master/debian/patches
  patch -p2 -i "${srcdir}/reproducible-import-libraries.patch"
  patch -p2 -i "${srcdir}/specify-timestamp.patch"

  # https://sourceware.org/bugzilla/show_bug.cgi?id=28138
  apply_patch_with_msg 2004-bfd-Close-the-file-descriptor-if-there-is-no-archive.patch

}

build() {
  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  export PATH=${_install_prefix}/bin:$PATH
  echo "PATH := $PATH"

#  /opt/i686-pc-mingw32/include/
#  /mingw-org/include/

  CC="${_target}-gcc" \
  ../${_realname}-${pkgver}/configure \
    --build=${_target} \
    --prefix=${_install_prefix} \
    --disable-werror \
    --enable-lto \
    --enable-gold \
    --with-sysroot=${_install_prefix} \
    --with-libiconv-prefix=${_install_prefix} \
    --with-libintl-prefix=${_install_prefix} \
    ${_conf} \
    --enable-nls \
    --disable-rpath \
    --disable-multilib \
    --enable-install-libbfd \
    --enable-install-libiberty \
    --enable-plugins \
    --enable-libssp \
    --enable-shared --enable-static \
    --enable-deterministic-archives

  make
}

#check() {
#  cd ${srcdir}/build-${MINGW_CHOST}
#
#  # unset LDFLAGS as testsuite makes assumptions about which ones are active
#  # do not abort on errors - manually check log files
#  make LDFLAGS="" -k check || true
#}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${_install_prefix}/{lib,include}/${_realname}
  mkdir -p ${pkgdir}${_install_prefix}/${_target}/include
  cp ${pkgdir}${_install_prefix}/include/ansidecl.h ${pkgdir}${_install_prefix}/${_target}/include/
  mv ${pkgdir}${_install_prefix}/include/*.h ${pkgdir}${_install_prefix}/include/${_realname}
  mv ${pkgdir}${_install_prefix}/lib/*.a ${pkgdir}${_install_prefix}/lib/${_realname}

  # https://github.com/msys2/MINGW-packages/issues/7890
#  rm "${pkgdir}${_install_prefix}/lib/bfd-plugins/libdep.a"

  find ${pkgdir}${_install_prefix}/share -type f -iname "opcodes.mo" -o -iname "bfd.mo" | xargs -rtl1 rm

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING      ${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.LIB  ${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING.LIB
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3     ${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING3
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3.LIB ${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING3.LIB

  rm -f "${pkgdir}${_install_prefix}/share/info/dir"
}
