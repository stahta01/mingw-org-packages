# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

# See also:
# https://github.com/archlinux/svntogit-community/blob/packages/mingw-w64-gcc/trunk/PKGBUILD
# https://github.com/msys2/MINGW-packages/blob/master/mingw-w64-gcc/PKGBUILD
# https://github.com/msys2/MSYS2-packages/blob/master/gcc/PKGBUILD
# https://github.com/msys2/MSYS2-packages/blob/master/mingw-w64-cross-gcc/PKGBUILD


PACKAGE_MINGW_ORG_PREFIX="mingw-org-$MSYSTEM_CARCH"
_target="i686-pc-mingw32"
_install_prefix="/mingw-org"

_enable_ada=yes
_enable_fortran=no

_base_pkg_name=gcc
pkgbase=mingw-org-${_base_pkg_name}3.4
pkgname=("${PACKAGE_MINGW_ORG_PREFIX}-${_base_pkg_name}")
pkgver=3.4.6
_sourcedir=${_base_pkg_name}-${pkgver}
pkgrel=1
pkgdesc="GNU Compiler Collection for mingw-org"
arch=('any')
mingw_arch=('mingw32')
url="https://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${PACKAGE_MINGW_ORG_PREFIX}-toolchain")
makedepends=("${PACKAGE_MINGW_ORG_PREFIX}-cc"
             "${PACKAGE_MINGW_ORG_PREFIX}-binutils"
             "${PACKAGE_MINGW_ORG_PREFIX}-wsl"
             "${PACKAGE_MINGW_ORG_PREFIX}-windows-default-manifest"
             "${PACKAGE_MINGW_ORG_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${PACKAGE_MINGW_ORG_PREFIX}-binutils"
         "${PACKAGE_MINGW_ORG_PREFIX}-wsl"
         "${PACKAGE_MINGW_ORG_PREFIX}-windows-default-manifest"
         "${PACKAGE_MINGW_ORG_PREFIX}-zlib")
provides=("${PACKAGE_MINGW_ORG_PREFIX}-cc")
conflicts=("${PACKAGE_MINGW_ORG_PREFIX}-cc")
options=('staticlibs' '!emptydirs' '!strip')
source=("https://ftp.gnu.org/gnu/gcc/${_base_pkg_name}-${pkgver%%+*}/${_base_pkg_name}-${pkgver}.tar.bz2"
        972-backport-SVN-117062.patch)
sha256sums=('7791a601878b765669022b8b3409fba33cc72f9e39340fec8af6d0e6f72dec39'
            '1c47a6da9592d3a6a5153ab817f37114aafee9b5c98c6dd20e7f2004c486e36c')
# =========================================== #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    if [ ! -f "${srcdir}/${_patch}" ]; then
      msg2 "Skipping ${_patch} because it is missing"
    elif patch --dry-run -Rbp1 -i "${srcdir}/${_patch}" > /dev/null 2>&1 ; then
      msg2 "Skipping ${_patch} because it likely was already applied"
    elif patch --dry-run -Nbp1 -i "${srcdir}/${_patch}" > /dev/null 2>&1 ; then
      msg2 "Applying ${_patch}"
      patch -Nbp1 -i "${srcdir}/${_patch}"
    else
      msg2 "Skipping ${_patch} because it will likely fail"
    fi
  done
}
# =========================================== #
prepare() {
  cd ${srcdir}/${_sourcedir}

  # Combination of build and runtime fixes
  apply_patch_with_msg \
    972-backport-SVN-117062.patch

  # Most likely run-time patch files

  # change hardcoded /mingw prefix to the real prefix
  local MINGW_NATIVE_PREFIX=$(cygpath -am ${_install_prefix})
  sed -i "s#\\/mingw\\/#${MINGW_NATIVE_PREFIX//\//\\/}\\/#g" gcc/config/i386/mingw32.h
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  export PATH=${_install_prefix}/bin:$PATH
  echo "PATH := $PATH"

  echo "GCC := " $(eval "${_target}-gcc --version | head -n 1 -")

  export LDFLAGS="-pipe"
  export CFLAGS="-pipe"
  export CXXFLAGS="-pipe"

  # Remove "-D__USE_MINGW_ANSI_STDIO=1" from CPPFLAGS
  CPPFLAGS=${CPPFLAGS/-D__USE_MINGW_ANSI_STDIO=1}
  echo "CPPFLAGS := $CPPFLAGS"

  local _languages="c"
  if [ "$_enable_ada" == "yes" ]; then
    _languages+=",ada"
  fi
  if [ "$_enable_fortran" == "yes" ]; then
    _languages+=",fortran"
  fi

  # so libgomp DLL gets built despide static libdl
  export lt_cv_deplibs_check_method='pass_all'

  LDFLAGS+=" -Wl,--disable-dynamicbase"

  # GCC 3.4.x has an optimization triggered build bug.
  CFLAGS+=" -O0 -I${_install_prefix}/include" \
  CC="${_target}-gcc" \
  ../${_sourcedir}/configure \
    --prefix=${_install_prefix} \
    --infodir=${_install_prefix}/share/info \
    --mandir=${_install_prefix}/share/man \
    --with-local-prefix=${_install_prefix}/local \
    --build=${_target} \
    --host=${_target} \
    --libexecdir=${_install_prefix}/lib \
    --disable-bootstrap \
    --disable-checking \
    --disable-build-format-warnings \
    --with-arch=i686 \
    --enable-languages=${_languages} \
    --enable-shared --enable-static \
    --disable-libatomic \
    --enable-threads=gnat \
    --disable-graphite \
    --enable-fully-dynamic-string \
    --disable-libstdcxx \
    --disable-libstdcxx-pch \
    --enable-sjlj-exceptions \
    --enable-large-address-aware \
    --disable-libgomp \
    --disable-libvtv \
    --disable-multilib \
    --disable-rpath \
    --disable-win32-registry \
    --disable-nls \
    --disable-werror \
    --disable-symvers \
    --with-system-zlib \
    --without-libiconv \
    --without-libintl \
    --with-pkgversion="Rev${pkgrel}, Built by Tims mingw-org project" \
    --with-bugurl="https://github.com/stahta01/mingw-org-packages/issues" \
    --with-boot-ldflags="${LDFLAGS} -static-libgcc" \
    --with-gnu-as --with-gnu-ld

  make -j1 V=1 all
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  mkdir -p ${pkgdir}${_install_prefix}/{bin,include,lib}
  mkdir -p ${pkgdir}${_install_prefix}/share/{info,man}

  make -j1 DESTDIR=${pkgdir} install

  # Install license files
  mkdir -p                                                  "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING"          "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}/COPYING"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.LIB"      "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}/COPYING.LIB"
}
