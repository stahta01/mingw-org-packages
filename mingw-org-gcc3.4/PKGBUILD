# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

# See also:
# https://github.com/archlinux/svntogit-community/blob/packages/mingw-w64-gcc/trunk/PKGBUILD
# https://github.com/msys2/MSYS2-packages/blob/master/gcc/PKGBUILD
# https://github.com/msys2/MINGW-packages/blob/master/mingw-w64-gcc/PKGBUILD


PACKAGE_MINGW_ORG_PREFIX="mingw-org-$MSYSTEM_CARCH"
_target="i686-pc-mingw32"
_install_prefix="/mingw-org"

_enable_ada=no
_enable_fortran=no

_base_pkg_name=gcc
pkgbase=mingw-org-${_base_pkg_name}3.4
pkgname=("${PACKAGE_MINGW_ORG_PREFIX}-${_base_pkg_name}")
pkgver=3.4.6
_sourcedir=${_base_pkg_name}-${pkgver}
pkgrel=1
pkgdesc="GCC for the mingw-org"
arch=('any')
mingw_arch=('mingw32')
url="https://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${PACKAGE_MINGW_ORG_PREFIX}-toolchain")
makedepends=("${PACKAGE_MINGW_ORG_PREFIX}-${_base_pkg_name}"
             "${PACKAGE_MINGW_ORG_PREFIX}-binutils"
             "${PACKAGE_MINGW_ORG_PREFIX}-wsl"
             "${PACKAGE_MINGW_ORG_PREFIX}-windows-default-manifest"
             "${PACKAGE_MINGW_ORG_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-autotools")
options=('staticlibs' '!emptydirs' '!strip')
source=("https://ftp.gnu.org/gnu/gcc/${_base_pkg_name}-${pkgver%%+*}/${_base_pkg_name}-${pkgver}.tar.bz2"
        701-libiberty-define-for-headers-that-libiberty-cannot-f.patch
        901-gcc-4.0-cfns-fix-mismatch-in-gnu_inline-attributes.patch)
sha256sums=('7791a601878b765669022b8b3409fba33cc72f9e39340fec8af6d0e6f72dec39'
            '530d50f66f7b36d33b6fa2a07718add7c617a6a28f908a1245a130491d81242d'
            'e0442e147448bd725d39bb8ce82b8e939ccc5b33b228388b43138022f1452b4c')

# =========================================== #

prepare() {
  cd ${srcdir}/${_sourcedir}

  # mingw.org needed or created patch files
  patch -Nbp1 -i ${srcdir}/701-libiberty-define-for-headers-that-libiberty-cannot-f.patch

  # Most likely run-time patch files
#  patch -Nbp1 -i ${srcdir}/140-fix-for-windows-not-minding-non-existent-parent-dirs.patch
#  patch -Nbp1 -i ${srcdir}/150-gcc-4.0-windows-lrealpath-no-force-lowercase-nor-backslash.patch

  # Most likely build-time patch files
  patch -Nbp1 -i ${srcdir}/901-gcc-4.0-cfns-fix-mismatch-in-gnu_inline-attributes.patch

  # do not expect ${prefix}/mingw symlink
  sed -i 's/${prefix}\/mingw\//${prefix}\//g' configure

  # change hardcoded /mingw prefix to the real prefix
  local MINGW_NATIVE_PREFIX=$(cygpath -am ${_install_prefix})
  sed -i "s#\\/mingw\\/#${MINGW_NATIVE_PREFIX//\//\\/}\\/#g" gcc/config/i386/mingw32.h

  # Fix "The directory that should contain system headers does not exist: /mingw/include"
  sed -i "s|/mingw/include|$_install_prefix/include|g" gcc/config/i386/t-mingw32
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  export PATH=${_install_prefix}/bin:$PATH
  echo "PATH := $PATH"

  export LDFLAGS="-pipe"
  export CFLAGS="-pipe"
  export CXXFLAGS="-pipe"

  # Remove "-D__USE_MINGW_ANSI_STDIO=1" from CPPFLAGS
  CPPFLAGS=${CPPFLAGS/-D__USE_MINGW_ANSI_STDIO=1}
  echo "CPPFLAGS := $CPPFLAGS"

  local _languages="c"
  if [ "$_enable_ada" == "yes" ]; then
    _languages+=",ada"
  fi
  if [ "$_enable_fortran" == "yes" ]; then
    _languages+=",fortran"
  fi

  # so libgomp DLL gets built despide static libdl
  export lt_cv_deplibs_check_method='pass_all'

  LDFLAGS+=" -Wl,--disable-dynamicbase"

  CPPFLAGS+=" -DLIBGCC2_HAS_XF_MODE=0" \
  CFLAGS+=" -I${_install_prefix}/include" \
  CC="${_target}-gcc" \
  ../${_sourcedir}/configure \
    --prefix=${_install_prefix} \
    --infodir=${_install_prefix}/share/info \
    --mandir=${_install_prefix}/share/man \
    --with-local-prefix=${_install_prefix}/local \
    --build=${_target} \
    --host=${_target} \
    --libexecdir=${_install_prefix}/lib \
    --disable-bootstrap \
    --disable-checking \
    --disable-build-format-warnings \
    --with-arch=i686 \
    --enable-languages=${_languages} \
    --enable-shared --enable-static \
    --disable-libatomic \
    --disable-threads \
    --disable-graphite \
    --enable-fully-dynamic-string \
    --disable-libstdcxx \
    --disable-libstdcxx-pch \
    --enable-sjlj-exceptions \
    --enable-large-address-aware \
    --disable-libgomp \
    --disable-libvtv \
    --disable-multilib \
    --disable-rpath \
    --disable-win32-registry \
    --disable-nls \
    --disable-werror \
    --disable-symvers \
    --with-system-zlib \
    --without-libiconv \
    --without-libintl \
    --with-pkgversion="Rev${pkgrel}, Built by Tims mingw-org project" \
    --with-bugurl="https://github.com/stahta01/mingw-org-packages/issues" \
    --with-boot-ldflags="${LDFLAGS} -static-libgcc" \
    --with-gnu-as --with-gnu-ld

  make -j1 V=1 all
}

package() {
  pkgdesc="GNU Compiler Collection for mingw-org"
  depends=("${PACKAGE_MINGW_ORG_PREFIX}-binutils"
           "${PACKAGE_MINGW_ORG_PREFIX}-wsl"
           "${PACKAGE_MINGW_ORG_PREFIX}-windows-default-manifest"
           "${PACKAGE_MINGW_ORG_PREFIX}-zlib")
  provides=("${PACKAGE_MINGW_ORG_PREFIX}-cc")
  conflicts=("${PACKAGE_MINGW_ORG_PREFIX}-cc")

  mkdir -p ${pkgdir}${_install_prefix}/{bin,include,lib}
  mkdir -p ${pkgdir}${_install_prefix}/share/{info,man}

  make -j1 DESTDIR=${srcdir} install

#  cd ${srcdir}${_install_prefix}
#  cp bin/cpp.exe                                        ${pkgdir}${_install_prefix}/bin/
#  cp bin/gcc.exe                                        ${pkgdir}${_install_prefix}/bin/
#  cp bin/gcc.exe                                        ${pkgdir}${_install_prefix}/bin/cc.exe
#  cp bin/gcov.exe                                       ${pkgdir}${_install_prefix}/bin/
#  cp bin/${_target}-gcc-${pkgver%%+*}.exe               ${pkgdir}${_install_prefix}/bin/
#  cp bin/${_target}-gcc.exe                             ${pkgdir}${_install_prefix}/bin/
#  mkdir -p ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/include
#  cp lib/gcc/${_target}/${pkgver%%+*}/include/*.h        ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/include/
#  cp -r lib/gcc/${_target}/${pkgver%%+*}/install-tools   ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/
#  cp lib/gcc/${_target}/${pkgver%%+*}/cc1.exe            ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/
#  cp lib/gcc/${_target}/${pkgver%%+*}/libgcov*           ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/
#  cp lib/gcc/${_target}/${pkgver%%+*}/libgcc*            ${pkgdir}${_install_prefix}/lib/gcc/${_target}/${pkgver%%+*}/

  # License files
  mkdir -p                                                  "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING"          "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}/COPYING"
  install -Dm644 "${srcdir}/${_sourcedir}/COPYING.LIB"      "${pkgdir}${_install_prefix}/share/licenses/${_base_pkg_name}/COPYING.LIB"
}
