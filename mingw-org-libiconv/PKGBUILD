# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

# MINGW_ARCH="mingw32" makepkg-mingw -Lf

PACKAGE_MINGW_ORG_PREFIX="mingw-org-$MSYSTEM_CARCH"
_target="i686-pc-mingw32"
_install_prefix="/mingw-org"
_cross_prefix="/opt"

_realname=libiconv
pkgbase=mingw-org-${_realname}
pkgname=(${PACKAGE_MINGW_ORG_PREFIX}-${_realname}) #  ${PACKAGE_MINGW_ORG_PREFIX}-iconv)
license=('GPL3')
pkgdesc='Character encoding conversion library and utility (mingw-org)'
pkgver=1.16
pkgrel=1
arch=('any')
mingw_arch=('mingw32')
url='https://www.gnu.org/software/libiconv/'
source=("https://ftp.gnu.org/pub/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-compile-relocatable-in-gnulib.mingw.patch
        0002-fix-cr-for-awk-in-configure.all.patch
        fix-pointer-buf.patch
        iconv.pc
        include-stdint.patch)
sha256sums=('e6a1b1b589654277ee790cce3734f07876ac4ccfaecbee8afa0b649cf529cc04'
            '7e905d21d670672b8a6a3bd80e5b8244b38bb7021a15fc7bdd0229125a3a68ee'
            'fe1a82f0836d65f895bebe9307bbe70ae53e5c872dd727f2042c46edc7cf060d'
            'fc1921f9d8ae21be0d1ee8a037fbb89aa066865f193a4883aa6e673827abba88'
            '56e7ec406bf42eb66b1d972f20d229f7a76ba00c38c7b4403bf348875704baae'
            'fb3cdb04dd0fb55406c5a2d6ccc78fca6ec284f4fd8db0cd56cbd8801a43213c')
options=('!libtool' 'staticlibs' '!strip')
#"${MINGW_PACKAGE_PREFIX}-cc"
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools")

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-compile-relocatable-in-gnulib.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-cr-for-awk-in-configure.all.patch
#  patch -p1 -i ${srcdir}/fix-pointer-buf.patch
#  patch -p1 -i ${srcdir}/include-stdint.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  export PATH=${_install_prefix}/bin:${_cross_prefix}/bin:$PATH
  echo "PATH := $PATH"

  LD="${_target}-ld" \
  AR="${_target}-ar" \
  CC="${_target}-gcc" \
  RC="${_target}-windres" \
  WINDRES="${_target}-windres" \
  CPPFLAGS="-I${_install_prefix}/include -Wall" \
  LDFLAGS="-L${_install_prefix}/lib" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${_install_prefix} \
    --build=${_target} \
    --docdir=${_install_prefix}/share/doc/libiconv \
    --enable-static \
    --enable-shared \
    --disable-extra-encodings \
    --enable-relocatable \
    --disable-rpath \
    --enable-silent-rules \
    --disable-nls

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package_libiconv() {
  pkgdesc='Character encoding conversion library (mingw-w64)'
  license=(LGPL2 documentation:'GPL3') # This is LGPL except for documentation, see README

  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
  rm -fr "${pkgdir}${_install_prefix}"/bin/*.exe
  rm -f  "${pkgdir}${_install_prefix}"/lib/charset.alias
  rm -fr "${pkgdir}${_install_prefix}"/share/locale
  rm -f  "${pkgdir}${_install_prefix}"/share/doc/libiconv/*.1.*
  rm -fr "${pkgdir}${_install_prefix}"/share/man/man1

  # Pkg-config file
  install -Dm644 "${srcdir}/iconv.pc"  "${pkgdir}${_install_prefix}/lib/pkgconfig/iconv.pc"
  sed -e "s|@PREFIX@|${_install_prefix}|g" \
      -e "s|@VERSION@|${pkgver}|g" \
      -i "${pkgdir}${_install_prefix}/lib/pkgconfig/iconv.pc"

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README"                 "${pkgdir}${_install_prefix}/share/licenses/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING"                "${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB"            "${pkgdir}${_install_prefix}/share/licenses/${_realname}/COPYING.LIB"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/libcharset/COPYING.LIB" "${pkgdir}${_install_prefix}/share/licenses/${_realname}/libcharset/COPYING.LIB"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${PACKAGE_MINGW_ORG_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
