# Maintainer:  Tim Stahlhut <stahta01@gmail.com>

_mingw_suff=mingw-org-cross
_install_prefix="/opt"

_realname=wsl
_sourcedir=${_realname}-git
pkgbase=mingw-org-cross-${_realname}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=5.4.3.g0c111acc
pkgrel=1
pkgdesc="Cross Windows System Libraries (WSL) w32api and mingwrt (mingw.org)"
arch=('i686' 'x86_64')
url='https://mingw.osdn.io/'
license=('LICENSE')
makedepends=('git' 'autotools')
_commit='0c111acc00bc15f5270535a71ee1601a3ed4a168'
source=("${_sourcedir}"::"git://git.osdn.net/gitroot/mingw/mingw-org-wsl.git#commit=${_commit}"
        'features.h')
sha256sums=('SKIP'
            '66b3fd839021d7aa4c4f41e2cd18f553aff354fe8205a66ef4c8a1d7133c39c6')

_targets="i686-pc-mingw32"

pkgver() {
  cd "${srcdir}/${_sourcedir}"

  local _version=$(head -n 34 VERSION.m4 | grep '__VERSION__' | sed -e 's/VERSION//' | sed -e 's/m4_define//' | sed -e 's/.* //' | tr -d '_,([])' | tr '\n' '.' | sed 's/.$/\n/')

  printf "%s.g%s" "$_version" $(git rev-parse --short=8 ${_commit})
}

prepare() {
  cd "${srcdir}/${_sourcedir}"

  cd w32api && autoconf -I ..
  cd ../mingwrt && autoconf -I ..
}

build() {
  for _target in ${_targets}; do
    cd "${srcdir}/${_sourcedir}/w32api"
    mkdir -p "${srcdir}/build-w32api-${_target}" && cd "${srcdir}/build-w32api-${_target}"

    # MSys2 default flags are wrong for this package
    CFLAGS='-pipe'
    CXXFLAGS='-pipe'

    msg "Configuring w32api for ${_target}"
    CC="${_target}-gcc" \
    ../${_sourcedir}/w32api/configure \
      --prefix="${_install_prefix}/${_target}" \
      --host=${_target}

    make

    msg "Installing local copy of w32api"
    cd "${srcdir}/build-w32api-${_target}"
    rm -fr ${srcdir}/temp-install-w32api${_install_prefix}
    mkdir -p ${srcdir}/temp-install-w32api${_install_prefix}/${_target}
    make DESTDIR=${srcdir}/temp-install-w32api install

    msg "Copy extra headers to local copy of w32api"
    cp --no-clobber ${srcdir}/build-w32api-${_target}/*.h ${srcdir}/temp-install-w32api${_install_prefix}/${_target}/include/

    cd "${srcdir}/${_sourcedir}/mingwrt"
    mkdir -p "${srcdir}/build-mingwrt-${_target}" && cd "${srcdir}/build-mingwrt-${_target}"

    msg "Configuring mingwrt for ${_target}"
    CFLAGS+=" -I${srcdir}/temp-install-w32api${_install_prefix}/${_target}/include" \
    CC="${_target}-gcc" \
    ../${_sourcedir}/mingwrt/configure \
      --prefix="${_install_prefix}/${_target}" \
      --host=${_target}

    make
  done
}

package() {
  for _target in ${_targets}; do
    cd "${srcdir}/build-w32api-${_target}"
    make install DESTDIR="${pkgdir}"
    cp --no-clobber "${srcdir}/features.h" "${pkgdir}${_install_prefix}/${_target}/include/"

    cd "${srcdir}/build-mingwrt-${_target}"
    make install DESTDIR="${pkgdir}"
  done
}
