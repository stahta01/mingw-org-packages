# Maintainer:  Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

# MINGW_ARCH="mingw32" makepkg-mingw -Lf

PACKAGE_MINGW_ORG_PREFIX="mingw-org-$MSYSTEM_CARCH"
_target="i686-pc-mingw32"
_install_prefix="/mingw-org"
_cross_prefix="/opt"

_realname=mpc
pkgbase=mingw-org-${_realname}
pkgname="${PACKAGE_MINGW_ORG_PREFIX}-${_realname}"
pkgver=1.2.1
pkgrel=1
pkgdesc="Multiple precision complex arithmetic library (mingw-org)"
url='http://www.multiprecision.org'
license=('LGPL')
arch=('any')
mingw_arch=('mingw32')
depends=("${PACKAGE_MINGW_ORG_PREFIX}-mpfr")
makedepends=("${PACKAGE_MINGW_ORG_PREFIX}-gmp" "${PACKAGE_MINGW_ORG_PREFIX}-mpfr" "${MINGW_PACKAGE_PREFIX}-autotools")
options=('staticlibs' '!emptydirs')
source=(#"http://www.multiprecision.org/mpc/download/${_realname}-${pkgver}.tar.gz"
        https://ftp.gnu.org/gnu/mpc/${_realname}-${pkgver}.tar.gz{,.sig})
sha256sums=('17503d2c395dfcf106b622dc142683c1199431d095367c6aacba6eec30340459'
            'SKIP')
validpgpkeys=('AD17A21EF8AED8F1CC02DBD9F7D5C9BF765C61E3')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  export PATH=${_install_prefix}/bin:${_cross_prefix}/bin:$PATH
  echo "PATH := $PATH"

  CC="${_target}-gcc" \
  ../${_realname}-${pkgver}/configure \
     --build=${MINGW_CHOST} \
    --prefix=${_install_prefix} \
    --enable-static \
    --disable-shared \
    --with-gmp=${_install_prefix} \
    --with-mpfr=${_install_prefix}
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}${_install_prefix}/share/info/dir"
}
